plugins {
	id 'java'
  id 'org.springframework.boot' version '3.4.8'
  id 'io.spring.dependency-management' version '1.1.0'
	id 'maven-publish'
}

// Configure all projects (root and subprojects)
allprojects {
    group = 'com.example'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

// Configure subprojects
subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

// Root project specific configuration
java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

dependencies {
	// Backend module dependencies (now as project dependencies)
	implementation project(':domain')
	implementation project(':api')
	implementation project(':persistent:mybatis')

	// Spring Boot dependencies
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-hateoas'
	implementation 'org.springframework.boot:spring-boot-starter-jersey'
	implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.4'
	implementation 'org.postgresql:postgresql'
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-database-postgresql'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

  // Test dependencies
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'com.h2database:h2'
  testImplementation 'org.springframework.security:spring-security-test'// Keep H2 for testing
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}
springBoot {
	buildInfo()
}
publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact bootJar
		}
	}
}
