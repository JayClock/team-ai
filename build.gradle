plugins {
    id "dev.nx.gradle.project-graph" version "0.1.10" apply false
    id 'io.spring.dependency-management' version '1.1.0' apply false
    id 'com.diffplug.spotless' version '6.25.0' apply false
}

allprojects {
    group = 'com.example'
    version = '0.0.1-SNAPSHOT'

    configurations.all {
        resolutionStrategy {
            cacheChangingModulesFor 10, 'minutes'
            cacheDynamicVersionsFor 10, 'minutes'
        }
    }

    repositories {
        mavenLocal()
        maven { url 'https://maven.aliyun.com/repository/public/' }
        mavenCentral()
        maven { url 'https://repo.spring.io/milestone' }
        maven { url 'https://repo.spring.io/snapshot' }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'maven-publish'
    apply plugin: 'dev.nx.gradle.project-graph'
    apply plugin: 'com.diffplug.spotless'

    spotless {
        java {
            target 'src/**/*.java'
            googleJavaFormat('1.22.0')
            importOrder()
            removeUnusedImports()
        }
        lineEndings 'UNIX'
    }

    tasks.named('check').configure {
        dependsOn tasks.named('spotlessCheck')
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs.add('-parameters')
        options.incremental = true
        options.encoding = 'UTF-8'
        options.fork = true
        options.forkOptions.jvmArgs += ['-Xmx512m']
    }

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.boot:spring-boot-dependencies:3.5.9'
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
        maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1

        systemProperty 'java.util.logging.config.file', "${projectDir}/src/test/resources/logging.properties"
    }

    tasks.withType(GenerateModuleMetadata) {
        suppressedValidationErrors.add('dependencies-without-versions')
    }
}
