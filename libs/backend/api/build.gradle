plugins {
    id 'org.asciidoctor.jvm.convert' version '4.0.4'
}

ext {
    snippetsDir = file('build/generated-snippets')
}

dependencies {
    implementation project(':backend:domain')
    implementation 'jakarta.inject:jakarta.inject-api'
    implementation 'jakarta.ws.rs:jakarta.ws.rs-api'
    implementation 'org.springframework.hateoas:spring-hateoas'
    implementation 'com.fasterxml.jackson.core:jackson-annotations'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'io.projectreactor:reactor-core'
    implementation 'org.glassfish.jersey.media:jersey-media-sse'
    testImplementation 'org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2'

    testImplementation 'org.springframework.boot:spring-boot-starter-hateoas'
    testImplementation 'org.springframework.boot:spring-boot-starter-jersey'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.rest-assured:rest-assured'
    testImplementation 'com.h2database:h2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Spring REST Docs with REST-assured
    testImplementation 'org.springframework.restdocs:spring-restdocs-restassured'
}

test {
    outputs.dir snippetsDir
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}

asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
    baseDirFollowsSourceFile()

    attributes(
        'snippets': snippetsDir,
        'project-version': project.version,
        'source-highlighter': 'highlight.js',
        'toc': 'left',
        'toclevels': 3,
        'sectlinks': true,
        'sectanchors': true
    )

    sources {
        include 'index.adoc'
    }

    outputDir = file('build/docs/api')
}

tasks.register('packageDocs', Copy) {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}") {
        into 'static/docs'
    }
    into layout.buildDirectory.dir('resources/main')
}

// Only package docs when explicitly requested
tasks.named('jar').configure {
    if (project.hasProperty('includeDocs')) {
        dependsOn 'packageDocs'
    }
}
